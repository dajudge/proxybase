plugins {
    id "com.palantir.git-version" version "0.12.2"
    id "com.jfrog.bintray" version "1.8.4" apply false
    id "com.github.hierynomus.license" version "0.15.0" apply false
}

def versionDetails = versionDetails()
def buildVersion = versionDetails.isCleanTag && gitVersion().startsWith("release/")
        ? gitVersion().substring("release/".length())
        : System.currentTimeMillis() + "-SNAPSHOT"
println "Build version: $buildVersion"

ext {
    libSlf4j = 'org.slf4j:slf4j-api:1.7.29'
}

subprojects {
    apply plugin: "java"
    apply plugin: "java-library"
    apply plugin: "maven-publish"
    apply plugin: "com.jfrog.bintray"

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        mavenLocal()
        mavenCentral()
    }

    version buildVersion
    apply from: "${rootProject.projectDir}/publishing.gradle"
    if (!version.endsWith("-SNAPSHOT")) {
        apply from: "${rootProject.projectDir}/bintray.gradle"
    } else {
        println "Not a tag build. Not configuring bintray."
    }

    test {
        testLogging {
            showStandardStreams = true
        }
    }
}

allprojects {
    apply plugin: "com.github.hierynomus.license"

    license {
        header file("${project.rootDir}/HEADER")
    }
}