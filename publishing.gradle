def scmUrl = 'https://github.com/dajudge/proxybase'
def pomConfig = {
    licenses {
        delegate.license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "alexstockinger"
            name "Alex Stockinger"
            email "mail@alexstockinger.de"
        }
    }

    scm {
        url scmUrl
    }
}

def projectGroupId = "com.dajudge.proxybase"
def projectArtifactId = project.name
def projectVersion = project.version
publishing {
    publications {
        ProxybasePublication(MavenPublication) {
            from components.java
            groupId projectGroupId
            artifactId projectArtifactId
            version projectVersion
            pom.withXml {
                def root = asNode()
                root.appendNode('description', "proxybase-${projectArtifactId} v${projectVersion}")
                root.appendNode('name', "proxybase-${projectArtifactId}")
                root.appendNode('url', scmUrl)
                root.children().last() + pomConfig
            }
        }
    }
}

publishToMavenLocal.doFirst {
    println "Publishing to maven local: $projectGroupId:$projectArtifactId:$projectVersion"
}